100 '####################
110 '# TITLE
120 '#  MSX BASIC FILE LAUNCHER
130 '# Version
140 '#  1.0.1
150 '# LICENSE
160 '#  These codes are licensed under CC0.
170 '#  http://creativecommons.org/publicdomain/zero/1.0/deed.ja
180 '#  Nasubi-K.T is marked with CC0 1.0
190 '####################
200 '## Initialize
210 SCREEN 1,0
220 COLOR 15,4,7
230 WIDTH 31
240 KEY OFF
250 CLS
260 DEFINT A-Z
270 XC=0
280 YC=0
290 AV=&H1800
300 WK$=""
310 FL$=""
320 SPRITE$(0)="@`pxp`@"
330 LOCATE 0,0
340 FILES "A:"
350 '## Main Loop
360 PUT SPRITE0,(XC*8,YC*8),8
370 S=STICK(0)+STICK(1)
380 XS=(S=7)-(S=3)
390 YS=(S=1)-(S=5)
400 AN=AV+(YC+YS)*32+XC+(XS*13)
410 MC=VPEEK(AN+1)
420 SA=STRIG(0)+STRIG(1)
430 IF SA THEN 580
440 XC=XC+(XS*13)
450 IF XC>13 THEN XC=13
460 IF XC<0 THEN XC=0
470 YC=YC+YS
480 IF YC>20 THEN YC=20
490 IF YC<0 THEN YC=0
500 '## Exit when ESC is pressed
510 I$=INKEY$
520 IF I$="" THEN 570
530 IF ASC(I$)<>27 THEN 570
540 PUT SPRITE0,(0,208),0
550 SCREEN 1,0
560 NEW
570 GOTO 350
580 '## Get selected filename
590 IF MC=32 THEN 350
600 FL$=""
610 FOR I=1 TO 12
620  FL$=FL$+CHR$(VPEEK(AN+I))
630 NEXT I
640 '## Executable file or not
650 WK$=RIGHT$(FL$,3)
660 IF WK$="BAS" THEN 690
670 IF WK$="EXE" THEN 690
680 GOTO 350
690 '## Execute the file
700 WK$="RUN "+FL$
710 LOCATE 0,22
720 PRINT WK$
730 PUT SPRITE0,(0,208),0
740 RUN FL$
